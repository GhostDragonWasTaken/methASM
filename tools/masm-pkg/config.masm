// Build config for masm-pkg
// masm.config format: key=value per line
// Optional; defaults used if missing or partial.

import "std/io";
import "std/conv";

export var CONFIG_FILE: string = "masm.config";

export struct BuildConfig {
  methasm: int8[128];
  entry: int8[128];
  stdlib: int8[128];
  package_dir: int8[128];
  output: int8[128];
  asm_out: int8[128];
  obj: int8[128];
  gc: int8[256];
  libs: int8[128];
  nasm_format: int8[32];
}

export function copy_str(dst: cstring, src: cstring, max_len: int32) {
  var i: int32 = 0;
  while (i < max_len - 1 && src[i] != 0) {
    dst[i] = src[i];
    i = i + 1;
  }
  dst[i] = 0;
}

export function set_config_field(config: BuildConfig*, key: cstring, val: cstring) {
  var k_methasm: string = "methasm";
  var k_entry: string = "entry";
  var k_stdlib: string = "stdlib";
  var k_packages: string = "package_dir";
  var k_output: string = "output";
  var k_asm: string = "asm";
  var k_obj: string = "obj";
  var k_gc: string = "gc";
  var k_libs: string = "libs";
  var k_nasm: string = "nasm_format";
  if (streq(key, cstr(k_methasm)) == 1) {
    copy_str(&config->methasm[0], val, 128);
  }
  if (streq(key, cstr(k_entry)) == 1) {
    copy_str(&config->entry[0], val, 128);
  }
  if (streq(key, cstr(k_stdlib)) == 1) {
    copy_str(&config->stdlib[0], val, 128);
  }
  if (streq(key, cstr(k_packages)) == 1) {
    copy_str(&config->package_dir[0], val, 128);
  }
  if (streq(key, cstr(k_output)) == 1) {
    copy_str(&config->output[0], val, 128);
  }
  if (streq(key, cstr(k_asm)) == 1) {
    copy_str(&config->asm_out[0], val, 128);
  }
  if (streq(key, cstr(k_obj)) == 1) {
    copy_str(&config->obj[0], val, 128);
  }
  if (streq(key, cstr(k_gc)) == 1) {
    copy_str(&config->gc[0], val, 256);
  }
  if (streq(key, cstr(k_libs)) == 1) {
    copy_str(&config->libs[0], val, 128);
  }
  if (streq(key, cstr(k_nasm)) == 1) {
    copy_str(&config->nasm_format[0], val, 32);
  }
}

export function get_default_config(config: BuildConfig*) -> int32 {
  if (config == 0) {
    return -1;
  }
  copy_str(&config->methasm[0], cstr("methasm"), 128);
  copy_str(&config->entry[0], cstr("main.masm"), 128);
  copy_str(&config->stdlib[0], cstr("stdlib"), 128);
  copy_str(&config->package_dir[0], cstr(".masm/packages"), 128);
  copy_str(&config->output[0], cstr("main.exe"), 128);
  copy_str(&config->asm_out[0], cstr("main.s"), 128);
  copy_str(&config->obj[0], cstr("main.o"), 128);
  copy_str(&config->gc[0], cstr("src/runtime/gc.c"), 256);
  copy_str(&config->libs[0], cstr("kernel32"), 128);
  copy_str(&config->nasm_format[0], cstr("win64"), 32);
  return 0;
}

export function parse_config_line(line: cstring, line_len: int32, config: BuildConfig*) -> int32 {
  if (line == 0 || config == 0) {
    return 0;
  }
  var i: int32 = 0;
  while (i < line_len && line[i] != 0) {
    if (is_space(line[i]) == 1) {
      i = i + 1;
    } else {
      break;
    }
  }
  if (i >= line_len || line[i] == 0 || line[i] == 10 || line[i] == 13 || line[i] == 35) {
    return 0;
  }
  var key_start: int32 = i;
  while (i < line_len && line[i] != 0 && line[i] != 61 && line[i] != 10 && line[i] != 13) {
    i = i + 1;
  }
  if (i >= line_len || line[i] != 61) {
    return 0;
  }
  var key_end: int32 = i;
  i = i + 1;
  var val_start: int32 = i;
  while (i < line_len && line[i] != 0 && line[i] != 10 && line[i] != 13) {
    i = i + 1;
  }
  var val_end: int32 = i;
  var key_len: int32 = key_end - key_start;
  var val_len: int32 = val_end - val_start;
  if (key_len <= 0 || key_len >= 64 || val_len <= 0 || val_len >= 256) {
    return 0;
  }
  var key_buf: int8[64];
  var j: int32 = 0;
  while (j < key_len) {
    key_buf[j] = line[key_start + j];
    j = j + 1;
  }
  key_buf[j] = 0;
  var val_buf: int8[256];
  j = 0;
  while (j < val_len) {
    val_buf[j] = line[val_start + j];
    j = j + 1;
  }
  val_buf[j] = 0;
  set_config_field(config, &key_buf[0], &val_buf[0]);
  return 1;
}

export function load_config(path: cstring, config: BuildConfig*) -> int32 {
  if (path == 0 || config == 0) {
    return -1;
  }
  get_default_config(config);
  var mode_r: cstring = cstr("r");
  var fp: cstring = fopen(path, mode_r);
  if (fp == 0) {
    return 0;
  }
  var line_buf: int8[512];
  while (fgets(&line_buf[0], 512, fp) != 0) {
    var len: int64 = strlen(&line_buf[0]);
    if (len > 511) {
      len = 511;
    }
    var len32: int32 = len;
    parse_config_line(&line_buf[0], len32, config);
  }
  fclose(fp);
  return 0;
}
